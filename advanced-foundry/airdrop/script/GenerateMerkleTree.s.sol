

// SPDX-License-Identifier: MIT

pragma solidity ^0.8.24;

import { Script } from "forge-std/Script.sol";
import { stdJson } from "forge-std/StdJson.sol";
import { console } from "forge-std/console.sol";
import { Merkle } from "murky/src/Merkle.sol";
import { ScriptHelper } from "murky/script/common/ScriptHelper.sol";


/* Merkle proof generator script
To use:
1. Run `forge script script/GenerateInput.s.sol` to generate the input file
2. Run `forge script script/MakeMerkle.s.sol`
3. The output file will be generated in /script/target/output.json */

/**
 * @title GenerateMerkleTree
 * @author Ciara Nightingale
 * @author Cyfrin
 *
 * Original Work by:
 * @author kootsZhin
 * @notice https://github.com/dmfxyz/murky
 */

contract GenerateMerkleTree is Script, ScriptHelper {
		using stdJson for string; // Enables us to use the json cheatcodes for strings

		Merkle private m = new Merkle(); // Instance of the merkle contract from Murky to do shit

		string private inputPath = "/script/target/input.json";
		string private outputPath = "/script/target/output.json";

		string private elements = vm.readFile(string.concat(vm.projectRoot(), inputPath)); // Get the absolute path 
		string[] private types = elements.readStringArray(".types"); // Gets the merkle tree leaf types from json using forge standard lib cheatcode 
		uint256 private count = elements.readUint(".count"); // Get the number of leaf nodes

		// Make three arrays the same size as the number of leaf nodes
		bytes32[] private leafs = new bytes32[](count);

		string[] private inputs = new string[](count);
		string[] private outputs = new string[](count);

		string private output;

		/// @dev Returns the JSON path of the input file
		// output file output ".values.some-address.some-amount"
		function getValuesByIndex(uint256 i, uint256 j) internal pure returns (string memory) {
				return string.concat(".values.", vm.toString(i), ".", vm.toString(j));
		} 

		/// @dev Generate the JSON entries for the output file
		function generateJsonEntries(string memory _inputs, string memory _proof, string memory _root, string memory _leaf)
				internal
				pure
				returns (string memory)
		{
				string memory result = string.concat(
						"{",
						"\"inputs\":",
						_inputs,
						",",
						"\"proof\":",
						_proof,
						",",
						"\"root\":\"",
						_root,
						"\",",
						"\"leaf\":\"",
						_leaf,
						"\"",
						"}"
				);

				return result;
		}

		/// @dev Read the input file and generate the Merkle proof, then write the output file
		function run() public {
				console.log("Generating Merkle Proof for %s", inputPath);

				for (uint256 i = 0; i < count; ++i) {
						string[] memory input = new string[](types.length); // stringified data (address and string both as strings)
						bytes32[] memory data = new bytes32[](types.length); // actual data as a bytes32

						for (uint256 j = 0; j < types.length; ++j) {
								if (compareStrings(types[j], "address")) {
										address value = elements.readAddress(getValuesByIndex(i, j));
										// You can't immediately cast straight to 32 bytes as an address is 20 bytes so first cast to uint160 (20 bytes) cast up to uint256 which is 32 bytes and finally to bytes32
										data[j] = bytes32(uint256(uint160(value))); 
										input[j] = vm.toString(value);
								} else if (compareStrings(types[j], "uint")) {
										uint256 value = vm.parseUint(elements.readString(getValuesByIndex(i, j)));
										data[j] = bytes32(value);
										input[j] = vm.toString(value);
								}
						}
						/* Create the hash for the merkle tree leaf node.
						Abi encode the data array (each element is a bytes32 representation for the address and the amount).
						Helper from Murky (ltrim64) Returns the bytes with the first 64 bytes removed .
						Ltrim64 removes the offset and length from the encoded bytes. There is an offset because the array is declared in memory.
						Hash the encoded address and amount.
						Bytes.concat turns from bytes32 to bytes.
						Hash again because preimage attack */
						leafs[i] = keccak256(bytes.concat(keccak256(ltrim64(abi.encode(data)))));
						/* Converts a string array into a JSON array string.
						Store the corresponding values/inputs for each leaf node */
						inputs[i] = stringArrayToString(input);
				}

				for (uint256 i = 0; i < count; ++i) {
						// Get proof gets the nodes needed for the proof & stringify (from helper lib)
						string memory proof = bytes32ArrayToString(m.getProof(leafs, i));
						// Get the root hash and stringify
						string memory root = vm.toString(m.getRoot(leafs));
						// Get the specific leaf working on
						string memory leaf = vm.toString(leafs[i]);
						// Get the singified input (address, amount)
						string memory input = inputs[i];

						// Generate the Json output file (tree dump)
						outputs[i] = generateJsonEntries(input, proof, root, leaf);
				}

				// Stringify the array of strings to a single string
				output = stringArrayToArrayString(outputs);
				// Write to the output file the stringified output json (tree dump)
				vm.writeFile(string.concat(vm.projectRoot(), outputPath), output);

				console.log("DONE: The output is found at %s", outputPath);
		}
}